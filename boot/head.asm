[SECTION .text]
[BITS 32]

extern main

global _start
_start:
    ; 尝试访问 1M 以上的内存 0x100000 以上
    ; 实模式下最高的地址总线 20 位，2^20 = 1M，不能访问超过 1M 的内存，超过就会进入回环，即地址 1M+1K=1K
    ; 打开 A20 后可以让超过 1M 的访问不回环
    ; 在进入保护模式后，我们定义段的页表单位为 4K，配合 20 位地址线，即 4K*(2^20) = 4G
    ; 故，我们可以访问 4G 地址空间了。


    ; 激活 8259A 中断控制器（PIC，Programmable Interrupt Controller）是为了在计算机系统中管理硬件中断请求，
    ; 并将中断信号适当地传递到处理器。8259A 是一种可编程的中断控制器，常用于 x86 计算机架构中，用来处理多个中断源。
    ; 在没有 8259A 芯片的情况下，处理器无法区分来自不同硬件设备的中断信号（如键盘、鼠标、磁盘等）。
    ; 8259A 可以帮助管理多个中断源，将它们映射到不同的中断请求（IRQ）上，并确定优先级。
    ; 配置8259a芯片，响应中断用
    .config_8259a:
        ; 向主发送ICW1
        mov al, 11h                 ; 000_1_0_0_0_1，必须为0_必须为1_边沿触发_8字节中断向量_级联8259a_需要发送ICW4
        out 20h, al

        ; 向从发送ICW1
        out 0a0h, al

        ; 向主发送ICW2
        mov al, 20h                 ; 主片中断号从 0x20 开始，即 32，因为 0-31 为系统保留的中断
        out 21h, al

        ; 向从发送ICW2
        mov al, 28h                 ; 继主片中断号从 0x28 开始，因为一个 8259a 芯片占 8 个中断。
        out 0a1h, al

        ; 向主发送ICW3
        mov al, 04h                 ; 00000100，也即 IR2 级联从片
        out 21h, al

        ; 向从发送ICW3
        mov al, 02h                 ; 00000010，即从片 IR1 与主片相连
        out 0A1h , al

        ; 向主发送ICW4
        mov al, 0b0000_0011         ; 0000 00_1_1，0000 00_自动EOI_X86模式
        out 021h, al

        ; 向从发送ICW4
        out 0A1h, al                ; 0000 00_1_1，0000 00_自动EOI_X86模式

    ; 屏蔽所有中断，只接收键盘中断
    .enable_8259a_main:
        mov al, 11111101b
        out 21h, al

    ; 屏蔽从芯片所有中断响应
    .enable_8259a_slave:
        mov al, 11111111b
        out 0A1h, al

    ; 修改栈顶
    mov esp, 0xA0000

    call main
    jmp $